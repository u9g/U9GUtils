plugins {
    id 'java'
    id 'maven-publish' // https://www.jetbrains.com/help/idea/add-a-gradle-library-to-the-maven-repository.html#publish
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'dev.u9g'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'u9g'
        url = 'https://maven.u9g.dev'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
    implementation 'com.manya:persistent-data-types:1.0.22'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

jar.configure {
    actions.clear()
    dependsOn shadowJar
}

shadowJar.configure {
    archiveClassifier.set('')
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = rootProject.name
            from components.java
        }
    }
}

